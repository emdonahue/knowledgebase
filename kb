#!/bin/zsh

#TODO add update language to query (-d -s?ort -u)
#TODO add options to ls to display different columns
#TODO add ls sorting based on note modification time
#TODO handle multiple filetypes that get added with same md5 (rtf and txt).
#TODO add search by md5

set -o errexit
set -o pipefail
if [[ "${TRACE-0}" == "1" ]]; then set -o xtrace; fi

set -o noclobber
set -o extendedglob
unset c #Reserved variable for inline comments
export LC_COLLATE=C

DIRS=(inbox md5 text bib org)
SCRIPT="$0"
E=(-e)

function disambiguate() { # Accepts md5s or filenames based on md5s and presents a formatted select, returning a single md5
    local MD5S=("${(@f)$(</dev/stdin)}")
    local PS3='Type a number to disambiguate: '
    [[ $#MD5S == 1 ]] && echo "$MD5S" && return
    select MD5 in "${(@f)$(printf '%s\n' "$MD5S[@]" | "$SCRIPT" ls -a)}"; do
	[[ $REPLY -le $#MD5S ]] && echo "$MD5S[$REPLY]" && break
    done < /dev/tty
}

function file2md5() { sed -E 's_^(.*/)*([[:alnum:]]+)(\..*)?$_\2_' }
function md52file() { sed -E 's_.*_'"$1"'/&'"${2:+.$2}"'_' }
function escapepattern() { sed -E 's/[^^]/[&]/g;s/\^/\\^/g' <<< "$1" }

function query() {
    case "$1" in
	'') cat;;
	-a) "$0" "$@[2,-1]";;
	-o) join -a1 -a2 - <("$0" -q "$@[2,-1]");;
	-q) ls bib | "$0" "$@[2,-1]";;
	*)
	    case "$2" in
		-eq|-ne)
		    md52file bib | xargs grep -Ei ${${2:/-eq/-l}:/-ne/-L} '^'"$1"$'\t''.*'"$(escapepattern "$3")" | file2md5 | "$0" "$@[4,-1]"
		    ;;
		*) echo "kb: malformed query: $@" >&2 && exit 1;;
	    esac 
	    ;;
    esac		
}

CMD="$1"

# Ensure proper directory structure
case "$CMD" in
    -h|--help|help|init);;
    *)
	for f in "${DIRS[@]}"; do
	    [[ -d "$f" ]] || {echo 'kb directory structure not detected. Use "kb init" to create a new directory structure.' >&2; exit 1}
	done
	;;
esac

[[ $# -ne 0 ]] && shift
case "$CMD" in
    --help|-h|help) #[SUBCOMMAND]; Prints help text for SUBCOMMAND. If SUBCOMMAND omitted, prints list of subcommands.
	[[ $# -eq 0 ]] && echo 'kb - Personal KnowledgeBase utility for organizing and searching PDFs, other text-based media, notes, etc.\nUsage: kb SUBCOMMAND [ARGUMENTS...]\n\nSubcommands:'
	sed -nE '/\s*'"$1"'\)\s#/'"${1:+,/^\s*;;\s*$/}"'{s/^[[:space:]]*([-|[:alnum:]]+)\)\s#([^;]*); (.*)/'"${${1-\t}:#$1}"'\1 \2\t\3/p; s/.*(\w+)[-+:]*=\w+ \$\{c#(.*); (.*)\}.*/\t-\1 \2\t\3/p}' "$SCRIPT"
	;;

    bib) #[OPTION...] QUERY; Adds or removes records from the bibliography files identified by QUERY.
	#echo "TEST: $@"
	zparseopts -D -E d+:=DELETIONS m+:=INSERTIONS
	
	#local BIBFLAGS=(-d -s)
	#echo '/'${(j:|:)${(@f)$(printf '%s\n' ${${DELETIONS:#-d}/=/$'\t'} | sed -E 's/[^^]/[&]/g;s/\^/\\^/g;s/\t\]/\\\\t].*/;s/^/\^/')}}'/d'
	DELETIONS=(${DELETIONS:+$(echo '/'${(j:|:)${(@f)$(printf '%s\n' ${${DELETIONS:#-d}/=/$'\t'} | sed -E 's/[^^]/[&]/g;s/\^/\\^/g;s/\t\]/\\\\t].*/;s/^/\^/')}}'/d')})
	INSERTIONS=('1 i '${^${INSERTIONS:#-m}/=/$'\t'})	
	#echo 'insert siz: '"${${E:^^INSERTIONS}[1,2]}"
	#echo 'final output: ' "${INSERTIONS:+${E:^^INSERTIONS}}"
	"$SCRIPT" md5 "$@" | md52file bib | xargs sed -Ei ${E:^DELETIONS}  ${INSERTIONS:+${E:^^INSERTIONS}}
	
	
	#echo '/^('${(j:|:)${DELETIONS:#-d}/=/$'\t'.*}')/d' >&2
	#grep -Eril '^ *\w+={.*'"$*.*}, *" bib | disambiguate_md5
	#${=EDITOR} bib/$(disambiguate ${(f)"$(grep -EZriH '^title={.*'"$*}," bib | sed -E 's|bib/(.*)\.bib\x00title\t(.*)|\2\t\1|')"} | sed -E 's/.*\t([^\t]+)$/\1/').bib
	;;
    
    import) #; Consumes files in inbox and generates appropriate archival copies, analysis extracts, and metadata containers.
	find inbox -type f -print0 | while read -d$'\0' f; do
	    IMPORT_MD5=$(md5sum "$f" | cut -f1 -d' ')
	    mv "$f" "md5/$IMPORT_MD5.$f:e"
	    [[ ! -f "bib/$IMPORT_MD5" ]] && echo 'title\t'"$f:t:r"'\nfilename\t'"$f:t:r" > "bib/$IMPORT_MD5"
	    [[ ! -f "org/$IMPORT_MD5.org" ]] && echo '[[file:../md5/'"$IMPORT_MD5"'.'"$f:e"']['"$f:t:r"']]' > "org/$IMPORT_MD5.org"
	done
	;;
    
    init) #; Generates a new directory structure for use by kb in the current directory.
	mkdir -p "${DIRS[@]}"
	;;

    ls) #[OPTION...] SEARCH; Lists files matching search criteria.
	#TODO use sed hold space to clip out title, author, year in desired order
	"$SCRIPT" md5 "$@" | md52file bib | xargs sed -nE 's/^title'$'\t''(.*)/\1/p'
	;;

    md5) #[QUERY]; Searches for files matching QUERY and prints a list of md5's for further processing. If QUERY is omitted, print all md5s.
	zparseopts -D -E i=INTERACTIVE
	query ${=${${:--q $1}:/-q -a/-a}:/-q -b/-b} "$@[2,-1]" | {[[ $INTERACTIVE ]] && disambiguate || cat}
	;;
    
    org) #QUERY; Opens the org notes file for the document matching QUERY using $EDITOR.
	${=EDITOR} org/$("$SCRIPT" md5 -i "$@").org
	;;
    
    *)	
	"$SCRIPT" help >&2
	exit 1
	;;
esac
